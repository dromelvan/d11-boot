/**
 * D11 Boot API Javascript client.
 */

plugins {
    id 'base'
    id "org.openapi.generator" version "4.2.3"
    id "com.github.node-gradle.node" version "2.2.4"
}

openApiGenerate() {
    generatorName = "javascript"
    inputSpec = project(':d11-boot-api').inputSpecFile.toString()
    outputDir = "$buildDir/generated"
    configOptions = [
        projectName: "d11-boot-api",
        usePromises: "true"
    ]
}

task npmInstallJSClient(type:NpmTask) {
    dependsOn tasks.openApiGenerate

    workingDir = file("$buildDir/generated")
    args = [ 'install' ]
}

task npmPackJSClient(type:NpmTask) {
    dependsOn tasks.npmInstallJSClient

    workingDir = file("$buildDir/generated")
    args = [ 'pack' ]

    doLast {
        ant.move file: "${buildDir}/generated/d11-boot-api-1.0.0.tgz", todir: "${buildDir}/libs"
    }
}

build {
    dependsOn tasks.npmPackJSClient
}
