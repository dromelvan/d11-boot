plugins {
    id 'java-library'
    id "org.openapi.generator" version "${openapiGeneratorVersion}"
}

dependencies {
    // Needed to compile classes generated by openApiGenerate.
    implementation "io.swagger.core.v3:swagger-annotations:${swaggerAnnotationsVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "jakarta.validation:jakarta.validation-api:${validationApiVersion}"


    // The spring generator adds a lot of unused imports.
    // This one being one of them. https://github.com/OpenAPITools/openapi-generator/issues/2901
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"
}

sourceSets {
    main {
        java {
            srcDir "${buildDir.absolutePath}/generated/src/main/java"
        }
    }
}

ext.inputSpecFile = "$projectDir/src/main/resources/openapi/d11-boot-openapi-v2.0.0.yaml"

openApiValidate {
    inputSpec = inputSpecFile
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = inputSpecFile
    outputDir = "$buildDir/generated"
    apiPackage = group + ".api.v2"
    modelPackage = group + ".api.v2.model"
    modelNameSuffix = "DTO"
    configOptions = [
            dateLibrary: "java8",
            useSpringBoot3: "true",
            serializableModel: "true",
            library: "spring-boot",
            skipDefaultInterface: "true",
            interfaceOnly: "true",
            booleanGetterPrefix: "is",
            generatedConstructorWithRequiredArgs: "false",
            useTags: "true",
            serializationLibrary: "jackson",
    ]

    typeMappings = [
            "Date": "LocalDate",
            "OffsetDateTime": "LocalDateTime"
    ]
    importMappings = [
            "java.time.OffsetDateTime": "java.time.LocalDateTime"
    ]
}

task openApiClean(type: Delete) {
    dependsOn tasks.openApiGenerate
    // We don't need the automagically generated ApiUtil
    delete fileTree('build\\generated\\') {
        include '**/ApiUtil.java'
    }
}

compileJava {
    dependsOn openApiClean
}
