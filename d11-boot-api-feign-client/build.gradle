/**
 * D11 Boot Admin API Client.
 */

plugins {
    id 'java-library'
    id "org.openapi.generator" version "5.3.0"
}

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

dependencies {
    api project(':d11-boot-api')

    api 'io.github.openfeign:feign-core:11.8'
    api 'io.github.openfeign:feign-jackson:11.8'
    api 'io.github.openfeign:feign-okhttp:11.8'
    api 'org.springframework.cloud:spring-cloud-starter-openfeign:3.1.0'

    // Needed to compile classes generated by openApiGenerate.
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.2'
    compileOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.2'
    compileOnly 'com.github.scribejava:scribejava-core:8.3.1'
}

sourceSets {
    main {
        java {
            srcDir "${buildDir.absolutePath}/generated/src/main/java"
        }
    }
}

openApiGenerate() {
    generatorName = "java"
    inputSpec = project(':d11-boot-api').inputSpecFile.toString()
    outputDir = "$buildDir/generated"
    invokerPackage = "org.d11.boot.client"
    apiPackage = "org.d11.boot.client.api"
    modelPackage = "org.d11.boot.api.model"
    modelNameSuffix = "DTO"
    configOptions = [
            dateLibrary: "java8",
            booleanGetterPrefix: "is",
            group: 'org.d11.boot.adminclient',
            library: "feign"
    ]
}

task openApiClean(type: Delete) {
    dependsOn tasks.openApiGenerate
    // We don't need the automagically generated ApiUtil
    delete fileTree('build\\generated\\src\\') {
        include '**/AndroidManifest.xml'
    }
    delete ('build\\generated\\src\\test')
    delete fileTree('build\\generated\\src\\main\\java\\org\\d11\\boot\\api\\model\\') {
        exclude '**/ApiResponse.java'
    }
}

compileJava {
    dependsOn openApiClean
}
